plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

dependencies {
  implementation platform('com.google.firebase:firebase-bom:33.10.0')
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}

android {
    namespace = "de.gruene.wkapp"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = "25.1.8937393"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "de.gruene.wkapp"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion

        if (project.hasProperty("VERSION_CODE") && project.hasProperty("VERSION_NAME")) {
            versionCode project.VERSION_CODE.toInteger()
            versionName project.VERSION_NAME
        } else {
            versionCode 1
            versionName "1.0"
        }

        manifestPlaceholders += [
                "appAuthRedirectScheme": "${applicationId}://oauthredirect"
        ]
    }

    signingConfigs {
        if (project.hasProperty("KEYSTORE_PATH")) { // Signing with env variables (CI)
            println "Signing using env variables"
            release {
                storeFile file("file://" + project.KEYSTORE_PATH)
                storePassword project.KEYSTORE_PASSWORD
                keyAlias project.KEYSTORE_KEY_ALIAS
                keyPassword project.KEYSTORE_KEY_PASSWORD
            }
        } else {
            println "Selecting debug keystore"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".development"
            resValue "string", "app_name", "B90 DEV"
        }
        release {
            resValue "string", "app_name", "B90/DIE GRÃœNEN"
            if (project.hasProperty("KEYSTORE_PATH")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }
}

flutter {
    source = "../.."
}
